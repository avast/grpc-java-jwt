plugins {
    id 'com.github.sherter.google-java-format' version '0.8' apply false
    id 'com.google.protobuf' version '0.8.12' apply false
    id 'com.avast.gradle.docker-compose' version '0.10.11' apply false
    id 'com.jfrog.bintray' version '1.8.5' apply false
}

allprojects {
    group 'com.avast.grpc.jwt'
    version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version
}

ext {
    grpcVersion = '1.29.0'
    protobufVersion = '3.11.4'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.sherter.google-java-format'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }
    
    dependencies {
        testCompile 'junit:junit:4.13'
        testCompile 'org.mockito:mockito-core:3.3.3'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    artifacts {
        archives sourcesJar
    }

    test {
        testLogging {
            events 'failed'
            exceptionFormat 'full'
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        configurations = ['archives']
        publish = true
        pkg {
            repo = 'maven'
            name = 'grpc-java-jwt'
            desc = 'Library that helps with authenticated communication in gRPC-Java based applications. It uses JSON Web Token.'
            userOrg = 'avast'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/avast/grpc-java-jwt.git'
            websiteUrl = 'https://github.com/avast/grpc-java-jwt'
            issueTrackerUrl = 'https://github.com/avast/grpc-java-jwt/issues'
            githubRepo = 'avast/grpc-java-jwt'
            labels = ['grpc', 'java', 'jwt', 'keycloak']
            version {
                name = project.version
                vcsTag = project.version
            }
        }
    }

}
